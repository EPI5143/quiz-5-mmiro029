*By Marcel Miron-Celis (8298424);
libname BigData '/folders/myshortcuts/Myfolders/Large databases/';
libname ClasData '/folders/myshortcuts/Myfolders/Large databases/Master Data Sets';
*Choose only unique admissions, this yields 24,531 unique admissions*;
Proc sort data=clasdata.nhrabstracts out=bigdata.SpineDS NODUPKEY;
by hraEncWID;
run;
*Choose only admissions between Jan 1st 2003 and Dec 31st 2004, this yields 2,227 unique admissions;
data bigdata.SpineDS;
set bigdata.spineDS;
if '01Jan2003'd<datepart(hraAdmDtm)<'31Dec2004'd;
run;
*Determine frequency of diabetes diagnosis and flatten diabetes diagnosis by encounter ID, Note that I cannot name indicator variable "DM" because that is a command on SAS University;
proc sort data=clasdata.nhrdiagnosis out=bigdata.diagnosis (rename=hdgHraEncWID=hraEncWID);
by hdgHraEncWID;
run;
data bigdata.Diabetes;
set bigdata.diagnosis;
by hraEncWID; 
if first.hraEncWID then do;
diabetes=0; count=0;
end;
if hdgcd in: ('250' 'E11' 'E10') then do;
diabetes=1; count=count+1; 
end;
if last.hraEncWID then output;
retain diabetes count;
run;
proc freq data=bigdata.diabetes;
tables diabetes count;
run;
*Merge spine data set and diabetes data set together, keeping only data between Jan 1st 2003 and Dec 31st 2004 ;
data bigdata.Linked;
merge bigdata.SpineDS bigdata.Diabetes;
by hraEncWID;
if '01Jan2003'd<datepart(hraAdmDtm)<'31Dec2004'd;
run;
*Frequency of diabetes diagnosis between Jan 1st 2003 and Dec 31st 2004;
proc freq data=bigdata.linked;
tables diabetes;
run;